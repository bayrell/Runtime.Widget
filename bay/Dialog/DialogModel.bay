/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Dialog;

use Runtime.Web.BaseModel;
use Runtime.Web.Events;
use Runtime.Widget.ButtonModel;
use Runtime.Widget.RowButtonsModel;
use Runtime.Widget.Styles;
use Runtime.Widget.WidgetResultModel;
use Runtime.Widget.Dialog.Dialog;


class DialogModel extends BaseModel
{
	bool is_open = false;
	string title = "";
	string content = "";
	string width = "";
	string component = classof Dialog;
	Events events = new Events;
	RowButtonsModel buttons;
	Styles styles = new Styles{ "prefix": "widget_dialog" };
	WidgetResultModel result = new WidgetResultModel;
	
	
	/**
	 * Setup widget params
	 */
	void setup(Dict params)
	{
		parent(params);
		
		if (params == null) return;
		if (params.has("content")) this.title = params.get("content");
		if (params.has("events")) this.events.setup(params.get("events"));
		if (params.has("styles")) this.styles.addStyles(params.get("styles"));
		if (params.has("title")) this.title = params.get("title");
		if (params.has("width")) this.title = params.get("width");
	}
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		this.buttons = this.addWidget(
			classof RowButtonsModel,
			{
				"widget_name": "buttons",
				"styles": ["@widget_dialog__buttons"],
			}
		);
	}
	
	
	/**
	 * Set title
	 */
	void setTitle(string title)
	{
		this.title = title;
	}
	
	
	/**
	 * Set content
	 */
	void setContent(string content)
	{
		this.content = content;
	}
	
	
	/**
	 * Set width
	 */
	void setWidth(string value)
	{
		this.width = value;
	}
	
	
	/**
	 * Show dialog
	 */
	void show()
	{
		this.is_open = true;
		this.result.clear();
	}
	
	
	/**
	 * Hide dialog
	 */
	void hide()
	{
		this.is_open = false;
	}
}