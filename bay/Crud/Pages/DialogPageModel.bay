/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Crud.Pages;

use Runtime.BaseModel;
use Runtime.Serializer;
use Runtime.Entity.Factory;
use Runtime.Web.ModelFactory;
use Runtime.Web.RenderContainer;
use Runtime.Widget.Crud.Pages.DialogPage;
use Runtime.Widget.Crud.CrudFactory;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Table.TableDialogModel;
use Runtime.Widget.RowButtonsModel;


class DialogPageModel extends BaseModel
{
	string component = classof DialogPage;
	CrudFactory crud = null;
	FormModel add_form = null;
	FormModel edit_form = null;
	FormModel delete_form = null;
	TableDialogModel table = null;
	RowButtonsModel top_buttons = null;
	
	
	/**
	 * Returns api name
	 */
	string getApiName(string action) => "";
	
	
	/**
	 * Returns primary key
	 */
	Collection getPrimaryKey() => [];
	
	
	/**
	 * Returns item fields
	 */
	Collection getItemFields() => [];
	
	
	/**
	 * Returns form fields
	 */
	Collection getFormFields(string action) => [];
	
	
	/**
	 * Returns table fields
	 */
	Collection getTableFields(string action) => [];
	
	
	/**
	 * Table title
	 */
	string getTableTitle(Dict params) => "";
	
	
	/**
	 * Returns routes
	 */
	string getParam(string name, Dict params = null)
	{
		/* Crud widget settings */
		if (name == "form.add.model.name") return classof FormModel;
		if (name == "form.edit.model.name") return classof FormModel;
		if (name == "form.delete.model.name") return classof FormModel;
		if (name == "table.model.name") return classof TableDialogModel;
		if (name == "table.page.name") return "p";
		
		/* Form storage settings */
		if (
			name == "form.add.storage" or
			name == "form.edit.storage" or
			name == "form.delete.storage"
		)
		{
			string storage_name = "Runtime.Widget.Form.FormSaveStorage";
			if (name == "form.delete.storage")
			{
				storage_name = "Runtime.Widget.Form.FormDeleteStorage";
			}
			return new Factory(
				storage_name,
				{
					"api_name": this.getApiName("save"),
				}
			);
		}
		
		/* Table storage settings */
		if (name == "table.storage")
		{
			return new Factory(
				"Runtime.Widget.Table.TableStorage",
				{
					"api_name": this.getApiName("search"),
				}
			);
		}
		
		return "";
	}
	
	
	/**
	 * Init widget settings
	 */
	void initParams(Dict params)
	{
		parent(params);
	}
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		/* Add form */
		this.add_form = this.addWidget(
			this.getParam("form.add.model.name"),
			{
				"widget_name": "form",
				"primary_key": this.getPrimaryKey(),
				"storage": this.getParam("form.add.storage", { "widget": this }),
				"fields": this.getFormFields("add_form"),
			}
		);
		
		/* Edit form */
		this.edit_form = this.addWidget(
			this.getParam("form.edit.model.name"),
			{
				"widget_name": "form",
				"primary_key": this.getPrimaryKey(),
				"storage": this.getParam("form.edit.storage", { "widget": this }),
				"fields": this.getFormFields("edit_form"),
			}
		);
		
		/* Delete form */
		this.delete_form = this.addWidget(
			this.getParam("form.delete.model.name"),
			{
				"widget_name": "delete_form",
				"primary_key": this.getPrimaryKey(),
				"storage": this.getParam("form.delete.storage", { "widget": this }),
			}
		);
		
		/* Add table */
		this.table = this.addWidget(
			this.getParam("table.model.name"),
			{
				"widget_name": "table",
				"styles": ["border"],
				
				/* Table title */
				"get_title": void (Dict params)
				{
					return this.getTableTitle(params);
				},
				
				/* Table storage */
				"storage": this.getParam("table.storage", { "widget": this }),
				
				/* Set page */
				"page": this.layout.request_query.get(this.getParam("table.page.name"), 1) - 1,
				"pagination_props":
				{
					"name": this.getParam("table.page.name"),
				},
				
				/* Form */
				"add_form": this.add_form,
				"edit_form": this.edit_form,
				"delete_form": this.delete_form,
				
				/* Table fields */
				"fields": this.getTableFields("table"),
			}
		);
		
		/* Add top buttons */
		this.top_buttons = this.addWidget(
			classof RowButtonsModel,
			{
				"widget_name": "top_buttons",
				"styles": ["top_buttons"],
				"buttons":
				[
					new ModelFactory
					(
						"Runtime.Widget.Table.AddButtonModel",
						{
							"table": this.table,
						}
					)
				],
			}
		);
	}
	
	
	/**
	 * Process frontend data
	 */
	void serialize(Serializer serializer, Map data)
	{
		if (serializer.isEncode())
		{
			serializer.process(this, "crud", data);
		}
		parent(serializer, data);
	}
	
	
	/**
	 * Build title
	 */
	void buildTitle(RenderContainer container)
	{
	}
}