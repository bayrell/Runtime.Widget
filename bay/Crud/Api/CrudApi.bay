/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Crud.Api;

use Runtime.lib;
use Runtime.BaseStruct;
use Runtime.ModelProxy;
use Runtime.Exceptions.ApiException;
use Runtime.Exceptions.RuntimeException;
use Runtime.ORM.Connection;
use Runtime.ORM.Cursor;
use Runtime.ORM.Query;
use Runtime.ORM.QueryResult;
use Runtime.ORM.Relation;
use Runtime.Web.ApiResult;
use Runtime.Web.BaseApi;
use Runtime.Web.BaseModel;
use Runtime.Web.Bus;
use Runtime.Web.RenderContainer;
use Runtime.Widget.Crud.Api.CrudRule;
use Runtime.Widget.Crud.Api.FieldResult;
use Runtime.Widget.Crud.Api.Filter;


class CrudApi extends BaseApi
{
	string action = "";
	int page = 0;
	int pages = 0;
	QueryResult items = null;
	Relation item = null;
	FieldResult field_result = new FieldResult();
	Map<QueryResult> dictionary = new Map;
	
	
	/**
	 * Returns api name
	 */
	pure string apiName() => "";
	
	
	/**
	 * Returns api methods
	 */
	pure string getApiMethod(string api_name)
	{
		return "";
	}
	
	
	/**
	 * Returns table name
	 */
	pure string getTableName() => "";
	
	
	/**
	 * Returns allowed fields
	 */
	Collection getAllowedFields(string kind)
	{
		if (kind == "save")
		{
			return this.getSaveFields();
		}
		if (kind == "item")
		{
			return this.getItemFields();
		}
		if (kind == "search")
		{
			return this.getSearchFields();
		}
		return [];
	}
	
	
	/**
	 * Returns search fields
	 */
	Collection getSearchFields() => this.getItemFields();
	
	
	/**
	 * Returns item fields
	 */
	Collection getItemFields() => [];
	
	
	/**
	 * Returns save fields
	 */
	Collection getSaveFields() => [];
	
	
	/**
	 * Returns database params
	 */
	Dict getDatabaseParams() => {};
	
	
	/**
	 * Returns database Connection
	 */
	Connection getDatabaseConnection()
	{
		Dict p = this.getDatabaseParams();
		string connection_name = p["name"];
		Connection conn = Connection::getConnection( connection_name );
		return conn;
	}
	
	
	/**
	 * Returns default limit
	 */
	int getDefaultLimit()
	{
		return 10;
	}
	
	
	/**
	 * Returns max limit
	 */
	int getMaxLimit()
	{
		return 100;
	}
	
	
	/**
	 * Returns page limit
	 */
	list getPageLimit()
	{
		int page = (int)(this.post_data["page"] |> default string 1);
		int limit = (int)(this.post_data["limit"] |> default string -1);
		int limit_default = this.getDefaultLimit();
		int limit_max = this.getMaxLimit();
		
		if (page <= 1) page = 1;
		if (limit < 0) limit = limit_default;
		if (limit > limit_max) limit = limit_max;
		
		return [page, limit];
	}
	
	
	/**
	 * Search init event
	 */
	async void onSearchInit()
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			await rule.onSearchInit(this);
		}
	}
	
	
	/**
	 * Search final event
	 */
	async void onSearchFinal()
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			await rule.onSearchFinal(this);
		}
	}
	
	
	/**
	 * Save init event
	 */
	async void onSaveInit()
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			await rule.onSaveInit(this);
		}
	}
	
	
	/**
	 * Save final event
	 */
	async void onSaveFinal()
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			await rule.onSaveFinal(this);
		}
	}
	
	
	/**
	 * Delete init event
	 */
	async void onDeleteInit()
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			await rule.onDeleteInit(this);
		}
	}
	
	
	/**
	 * Delete final event
	 */
	async void onDeleteFinal()
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			await rule.onDeleteFinal(this);
		}
	}
	
	
	/**
	 * Returns rules
	 */
	Collection<CrudRule> getRules() =>
	[
	];
	
	
	/**
	 * Build filter
	 */
	Query buildFilter(Query q)
	{
		Collection<string> filter_fields = this.getRules()
			|> .filter( lib::isInstance(classof Filter) )
		;
		
		Vector query_filter = new Vector();
		Collection request_filter = this.post_data["filter"];
		if (request_filter instanceof Collection)
		{
			for (int i=0; i<request_filter.count(); i++)
			{
				Collection filter = request_filter[i];
				if (filter instanceof Collection and filter.count() == 3)
				{
					string field_name = filter[0];
					string field_op = filter[1];
					string field_value = filter[2];
					if (
						rtl::isString(field_name) and
						rtl::isString(field_op) and
						(rtl::isString(field_value) or rtl::isInt(field_value))
					)
					{
						Filter rule = filter_fields.findItem(
							lib::equalAttr("api_name", field_name)
						);
						
						if (rule)
						{
							query_filter.pushValue([rule.field_name, field_op, field_value]);
						}
					}
				}
			}
		}
		
		return q.setFilter(query_filter);
	}
	
	
	/**
	 * Build search query
	 */
	async Query buildSearchQuery(Query q) => q;
	
	
	/**
	 * Init api
	 */
	async void initApi(string action)
	{
		this.action = action;
	}
	
	
	/**
	 * Search items
	 */
	async void searchItems()
	{
		list res = this.getPageLimit();
		int page = res[0];
		int limit = res[1];
		int pages = 0;
		
		if (this.action == "actionSearchOne")
		{
			limit = 1;
		}
		
		this.page = page;
		this.limit = limit;
		
		/* Build query */
		string table_name = this.getTableName();
		Query q = new Query
			|> .select(table_name)
			|> .page(page - 1, limit)
		;
		
		/* Setup calc found rows */
		if (this.action == "actionSearchOne")
		{
			q = q.calcFoundRows(false);
		}
		else if (this.action == "actionSearch")
		{
			if (limit > 0)
			{
				q = q.calcFoundRows(true);
			}
		}
		
		/* Filter */
		q = this.buildFilter(q);
		q = this.buildSearchQuery(q);
		
		/* Execute query */
		Connection conn = this.getDatabaseConnection();
		Cursor c = await conn.execute(q, this.getDatabaseParams());
		QueryResult items = await c.fetchAll();
		if (q._limit > 0)
		{
			page = await c.getPage() + 1;
			pages = await c.getPages();
		}
		await c.close();
		
		this.page = page;
		this.pages = pages;
		this.items = items;
	}
	
	
	/**
	 * Action search
	 */
	void actionSearch()
	{
		/* Init api */
		await this.initApi("actionSearch");
		if (this.container.response.error_name != "") return;
		
		/* Search before event */
		await this.onSearchInit();
		if (this.container.response.error_name != "") return;
		
		/* Search items */
		await this.searchItems();
		
		/* Transform items */
		Collection items = this.items.toDict( this.getAllowedFields("search") );
		
		/* Set answer */
		this.container.success({
			"data":
			{
				"page": this.page,
				"pages": this.pages,
				"limit": this.limit,
				"items": items,
				"dictionary": {},
			}
		});
		
		/* Search after event */
		await this.onSearchFinal();
	}
	
	
	/**
	 * Action search
	 */
	async void actionSearchOne()
	{
		/* Init api */
		await this.initApi("actionSearchOne");
		if (this.container.response.error_name != "") return;
		
		/* Search before event */
		await this.onSearchInit();
		if (this.container.response.error_name != "") return;
		
		/* Search items */
		await this.searchItems();
		
		/* Get item */
		this.item = this.items.count() > 0 ? this.items[0] : null;
		if (this.item == null)
		{
			this.container.fail( new ApiException("Item not found") );
			return ;
		}
		
		/* Transform items */
		BaseStruct model = this.item.toDict( this.getAllowedFields("search") );
		Dict item_pk = this.item.getPrimaryKey();
		
		/* Set answer */
		this.container.success({
			"data":
			{
				"pk": item_pk,
				"item": model,
				"dictionary": {},
			}
		});
		
		/* Search after event */
		await this.onSearchFinal();
	}
	
	
	/**
	 * Find item
	 */
	async Relation findItem(Dict pk)
	{
		/* Query to database */
		string table_name = this.getTableName();
		
		/* Get primary key */
		Collection filter = Relation::getPrimaryFilter(table_name, pk, true);
		
		/* Build query */
		Query q = new Query
			|> .select(table_name)
			|> .setFilter(filter)
		;
		q = this.buildSearchQuery(q);
		q = q.limit(1);
		
		/* Execute query */
		Connection conn = this.getDatabaseConnection();
		Cursor c = await conn.execute(q, this.getDatabaseParams());
		Relation item = await c.fetchRelation();
		await c.close();
		
		if (!item)
		{
			return null;
		}
		
		return item;
	}
	
	
	/**
	 * Action dictionary
	 */
	async void actionDictionary()
	{
		/* Init api */
		await this.initApi("actionDictionary");
		if (this.container.response.error_name != "") return;
		
		/* Set answer */
		this.container.success({
			"data":
			{
				"dictionary": {},
			}
		});
		
		/* After event */
		await this.onSearchFinal();
	}
	
	
	/**
	 * Action item
	 */
	async void actionItem()
	{
		Dict pk = this.post_data["pk"];
		
		/* Init api */
		await this.initApi("actionItem");
		if (this.container.response.error_name != "") return;
		
		/* Before event */
		await this.onSearchInit();
		if (this.container.response.error_name != "") return;
		
		/* Find item */
		this.item = await this.findItem(pk);
		if (this.item == null)
		{
			this.container.fail( new ApiException("Item not found") );
			return ;
		}
		this.items = [ this.item ];
		
		/* Transform items */
		BaseStruct model = this.item.toDict( this.getAllowedFields("item") );
		Dict item_pk = this.item.getPrimaryKey();
		
		/* Set answer */
		this.container.success({
			"data":
			{
				"pk": item_pk,
				"item": model,
				"dictionary": {},
			}
		});
		
		/* After event */
		await this.onSearchFinal();
	}
	
	
	/**
	 * Create item
	 */
	async Relation createItem() => new Relation( this.getTableName() );
	
	
	/**
	 * Before update item event
	 */
	async void onSaveBefore(Relation item, Dict update_data)
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			await rule.onSaveBefore(this, item, update_data);
		}
	}
	
	
	/**
	 * After update item event
	 */
	async void onSaveAfter(Relation item, Dict update_data)
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			await rule.onSaveAfter(this, item, update_data);
		}
	}
	
	
	/**
	 * Update item
	 */
	async void updateItem(Relation item, Dict update_data)
	{
		Connection conn = this.getDatabaseConnection();
		Collection keys = update_data.keys();
		if (keys.count() > 0)
		{
			Collection items = this.getAllowedFields("save");
			
			for (int i=0; i<keys.count(); i++)
			{
				string field_name = keys[i];
				if (items.indexOf(field_name) >= 0)
				{
					item.set(field_name, update_data[field_name]);
				}
			}
			
			/* Before item */
			await this.onSaveBefore(item, update_data);
			if (this.container.response.error_name != "") return;
			if (not this.field_result.isSuccess())
			{
				this.container.fail( this.field_result.getResult() );
				return;
			}
			
			/* Save item */
			await item.save(conn);
			
			/* Refresh item */
			Dict pk = item.getPrimaryKey();
			item = await this.findItem(pk);
			
			/* Before item */
			await this.onSaveAfter(item, update_data);
		}
	}
	
	
	/**
	 * Validate item
	 */
	async Dict validateItem(Dict update_data)
	{
		Collection<CrudRule> rules = this.getRules();
		for (int i=0; i<rules.count(); i++)
		{
			CrudRule rule = rules[i];
			update_data = await rule.validateItem(this, update_data);
		}
		return update_data;
	}
	
	
	/**
	 * Action save
	 */
	async void actionSave()
	{
		Dict pk = this.post_data["pk"];
		Dict update_data = this.post_data["item"];
		
		/* Init api */
		await this.initApi("actionSave");
		if (this.container.response.error_name != "") return;
		
		/* Before event */
		await this.onSaveInit();
		if (this.container.response.error_name != "") return;
		
		/* Validate data */
		update_data = await this.validateItem(update_data);
		if (not this.field_result.isSuccess())
		{
			this.container.fail( this.field_result.getResult() );
			return;
		}
		if (this.container.response.error_name != "") return;
		
		/* Find item */
		if (pk != null)
		{
			this.item = await this.findItem(pk);
		}
		else
		{
			this.item = await this.createItem();
		}
		
		/* Item not found */
		if (this.item == null)
		{
			this.container.fail( new ApiException("Item not found") );
			return;
		}
		
		/* Update item */
		await this.updateItem(this.item, update_data);
		if (this.container.response.error_name != "") return;
		
		/* Transform items */
		BaseStruct model = this.item.toDict( this.getAllowedFields("item") );
		Dict item_pk = this.item.getPrimaryKey();
		
		/* Set api result */
		this.container.success({
			"data":
			{
				"pk": item_pk,
				"item": model,
				"dictionary": {},
			}
		});
		
		/* After event */
		await this.onSaveFinal();
	}
	
	
	/**
	 * Delete item
	 */
	async void deleteItem(Relation item)
	{
		Connection conn = this.getDatabaseConnection();
		item.delete(conn);
	}
	
	
	/**
	 * Action delete
	 */
	async void actionDelete()
	{
		Dict pk = this.post_data["pk"];
		
		/* Init api */
		await this.initApi("actionDelete");
		if (this.container.response.error_name != "") return;
		
		/* Before event */
		await this.onDeleteInit();
		if (this.container.response.error_name != "") return;
		
		/* Find item */
		this.item = await this.findItem(pk);
		if (this.item == null)
		{
			this.container.fail( new ApiException("Item not found") );
			return;
		}
		
		/* Delete item */
		await this.deleteItem(this.item);
		if (this.container.response.error_name != "") return;
		
		/* Transform items */
		BaseStruct model = this.item.toDict( this.getAllowedFields("item") );
		Dict item_pk = this.item.getPrimaryKey();
		
		/* Set api result */
		this.container.success({
			"data":
			{
				"pk": item_pk,
				"item": model,
				"dictionary": {},
			}
		});
		
		/* Before event */
		await this.onDeleteFinal();
	}
	
}
