/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Crud;

use Runtime.BaseObject;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Web.ApiResult;
use Runtime.Widget.Crud.RulesManager;


class CrudService extends BaseObject
{
	/* Rules */
	RulesManager rules = new RulesManager();
	
	/* Item */
	Dict pk = null;
	Dict data = null;
	Dict item = null;
	bool is_create = false;
	
	/* Search */
	Dict search_params = null;
	Collection<Dict> items = null;
	int page = 0;
	int pages = 0;
	int limit = 0;
	
	
	/**
	 * Constructor
	 */
	void constructor()
	{
		parent();
		this.initRules();
	}
	
	
	/**
	 * Returns true if create
	 */
	bool isCreate() => this.is_create;
	
	
	/**
	 * Returns true if update
	 */
	bool isUpdate() => not this.is_create;
	
	
	/**
	 * Returns true if search
	 */
	bool isSearch() => this.search_params != null;
	
	
	/**
	 * Set create
	 */
	void setCreate(bool value)
	{
		this.is_create = value;
		this.rules.setCreate(value);
	}
	
	
	/**
	 * New item
	 */
	async Dict newItem() => null;
	
	
	/**
	 * Find item
	 */
	async Dict findItem(Dict pk) => null;
	
	
	/**
	 * Init rules
	 */
	void initRules()
	{
	}
	
	
	/**
	 * Returns save fields
	 */
	Collection getSaveFields() =>
	[
	];
	
	
	/**
	 * Returns primary key
	 */
	Dict getPrimaryKey(Dict item) => null;
	
	
	/**
	 * Set new item
	 */
	void setItem(Dict item, bool is_new = false)
	{
		this.item = item;
		this.pk = item ? this.getPrimaryKey(this.item) : null;
		this.setCreate(is_new);
	}
	
	
	/**
	 * Set item
	 */
	void setItemValue(Dict item, string key, var value)
	{
		item.set(key, value);
	}
	
	
	/**
	 * Set item data
	 */
	void setItemData(Dict item, Dict data)
	{
		if (not data) return;
		
		Collection<string> keys = this.getSaveFields();
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys.get(i);
			if (not data.has(key)) continue;
			var value = data.get(key);
			this.setItemValue(item, key, value);
		}
	}
	
	
	/**
	 * Convert item
	 */
	Map convertItem(Dict item, Collection fields)
	{
		return item.intersect(fields);
	}
	
	
	/**
	 * Load item
	 */
	async void loadItem(Dict pk, bool create_instance = false)
	{
		if (pk != null and pk instanceof Dict)
		{
			var item = await this.findItem(pk);
			this.setItem(item, false);
		}
		else
		{
			if (create_instance)
			{
				this.setItem(this.newItem(), true);
			}
		}
		
		/* Item not found */
		if (this.item == null)
		{
			throw new ApiError(new ItemNotFound());
		}
	}
	
	
	/**
	 * Validate data
	 */
	async void validate()
	{
		await this.rules.validate(this.data);
	}
	
	
	/**
	 * Before search
	 */
	async void onSearchBefore()
	{
		await this.rules.onSearchBefore(this);
	}
	
	
	/**
	 * After search
	 */
	async void onSearchAfter()
	{
		await this.rules.onSearchAfter(this);
	}
	
	
	/**
	 * Load items
	 */
	async void loadItems()
	{
		this.items = [];
		this.page = 0;
		this.pages = 0;
		this.limit = 0;
	}
	
	
	/**
	 * Search items
	 */
	async void search(Dict params)
	{
		/* Set search params */
		this.search_params = params;
		
		/* Before search */
		await this.onSearchBefore();
		
		/* Load items */
		await this.loadItems();
		
		/* After search */
		await this.onSearchAfter();
	}
	
	
	/**
	 * Search item
	 */
	async void searchItem(Dict pk)
	{
		/* Before search */
		await this.onSearchBefore();
		
		/* Load item */
		await this.loadItem(pk, false);
		
		/* After search */
		await this.onSearchAfter();
	}
	
	
	/**
	 * Before save
	 */
	async void onSaveBefore()
	{
		await this.rules.onSaveBefore(this);
	}
	
	
	/**
	 * After save
	 */
	async void onSaveAfter()
	{
		await this.rules.onSaveAfter(this);
	}
	
	
	/**
	 * Save item
	 */
	async void saveItem()
	{
	}
	
	
	/**
	 * Save
	 */
	async bool save(Dict data)
	{
		if (data == null or not(data instanceof Dict)) data = {};
		
		/* Validate item */
		this.data = data.copy();
		await this.validate();
		if (not this.rules.correct()) return false;
		
		/* Before save */
		await this.onSaveBefore();
		if (not this.rules.correct()) return;
		
		/* Set item data */
		this.setItemData(this.item, this.data);
		
		/* Save item */
		await this.saveItem();
		
		/* After save */
		await this.onSaveAfter();
		
		return true;
	}
	
	
	/**
	 * Before delete
	 */
	async void onDeleteBefore()
	{
		await this.rules.onDeleteBefore(this);
	}
	
	
	/**
	 * After delete
	 */
	async void onDeleteAfter()
	{
		await this.rules.onDeleteAfter(this);
	}
	
	
	/**
	 * Delete item
	 */
	async void deleteItem()
	{
	}
	
	
	/**
	 * Delete
	 */
	async bool delete()
	{
		/* Before delete */
		await this.onDeleteBefore();
		
		/* Delete item */
		await this.deleteItem();
		
		/* After delete */
		await this.onDeleteAfter();
		
		return true;
	}
}