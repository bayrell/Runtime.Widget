/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Crud;

use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.Web.BaseApi;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Widget.Crud.CrudFactory;
use Runtime.Widget.Crud.CrudService;
use Runtime.Widget.Crud.FieldException;


class SaveApi extends BaseApi
{
	/**
	 * Returns service
	 */
	CrudService createService() => null;
	
	
	/**
	 * Returns item fields
	 */
	Collection getItemFields() => [];
	
	
	/**
	 * Setup params
	 */
	void setup(Dict params)
	{
		parent(params);
		
		if (params == null) return;
		if (params.has("crud")) this.crud = params.get("crud");
	}
	
	
	/**
	 * Build error
	 */
	void buildError(CrudService service)
	{
		/* Check if item is exists */
		if (not service.item)
		{
			throw new ApiError(new ItemNotFound());
		}
		
		/* Validate error */
		if (not service.rules.correct())
		{
			throw new ApiError(new FieldException());
		}
	}
	
	
	/**
	 * Build result
	 */
	async void buildResult(CrudService service)
	{
		this.result.data.set("fields", service.rules.getFields());
		
		/* Check error */
		this.buildError(service);
		
		/* Convert item */
		Collection fields = this.getItemFields();
		Dict item = service.convertItem(service.item, fields);
		Dict pk = service.getPrimaryKey(service.item);
		
		/* Setup result */
		this.result.data.set("pk", pk);
		this.result.data.set("item", item);
		
		/* Success */
		this.success();
	}
	
	
	/**
	 * Action item
	 */
	async void actionItem()
	{
		/* Create service */
		CrudService service = this.createService();
		if (not service)
		{
			throw new ApiError(new RuntimeException("Service not found"));
		}
		
		/* Load item */
		await service.searchItem(this.post_data.get("pk"));
		
		/* Build result */
		this.buildResult(service);
	}
	
	
	/**
	 * Action create
	 */
	@ApiMethod
	async void actionCreate()
	{
		await this.actionSave();
	}
	
	
	/**
	 * Action save
	 */
	@ApiMethod
	async void actionSave()
	{
		/* Create service */
		CrudService service = this.createService();
		if (not service)
		{
			throw new ApiError(new RuntimeException("Service not found"));
		}
		
		/* Load item */
		await service.loadItem(this.post_data.get("pk"), true);
		
		/* Save item */
		await service.save(this.post_data.get("item"));
		
		/* Build result */
		this.buildResult(service);
	}
	
	
	/**
	 * Action delete
	 */
	@ApiMethod
	async void actionDelete()
	{
		/* Create service */
		CrudService service = this.createService();
		if (not service)
		{
			throw new ApiError(new RuntimeException("Service not found"));
		}
		
		/* Load item */
		await service.loadItem(this.post_data.get("pk"));
		
		/* Delete item */
		await service.delete();
		
		/* Build result */
		this.buildResult(service);
	}
}