/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Crud;

use Runtime.lib;
use Runtime.BaseStruct;
use Runtime.Serializer;
use Runtime.Web.BaseModel;
use Runtime.Web.Events;
use Runtime.Web.RenderContainer;
use Runtime.Widget.WidgetResultModel;
use Runtime.Widget.Crud.ApiResult;
use Runtime.Widget.Crud.CrudApiStorage;


class Dictionary extends BaseModel
{
	Vector options = [];
	Events events = new Events;
	WidgetResultModel result = new WidgetResultModel;
	CrudApiStorage storage = null;
	fn filter = null;
	fn transform = null;
	
	
	/**
	 * Setup widget params
	 */
	void setup(Dict params)
	{
		parent(params);
		
		if (params == null) return;
		if (params.has("storage")) this.storage = params.get("storage");
		if (params.has("filter")) this.filter = params.get("filter");
		if (params.has("transform")) this.transform = params.get("transform");
	}
	
	
	/**
	 * Returns options
	 */
	Vector getOptions() => this.options;
	
	
	/**
	 * Set api result
	 */
	void setApiResult(ApiResult res, string action = "")
	{
		this.result.setApiResult(res);
	}
	
	
	/**
	 * Reload options
	 */
	void reloadOptions()
	{
		Vector items = this.storage.items;
		if (items == null) return;
		
		if (this.filter)
		{
			items = items.filter( this.filter );
		}
		if (this.transform)
		{
			items = items.map( this.transform );
		}
		this.options = items;
	}
	
	
	/**
	 * Load table data
	 */
	async void loadData(RenderContainer container)
	{
		await parent(container);
		this.storage.setLimit(-1);
		ApiResult res = await this.storage.doSearch();
		this.setApiResult(res, "search");
		this.reloadOptions();
	}
	
	
	/**
	 * Process frontend data
	 */
	void serialize(Serializer serializer, Map data)
	{
		serializer.process(this, "options", data);
		serializer.process(this, "result", data);
		serializer.process(this, "storage", data);
		parent(serializer, data);
	}
	
	
	/**
	 * On change
	 */
	void onValueChange(string value, var data = null)
	{
		this.events.emit("onValueChange", [value, data]);
	}
}
