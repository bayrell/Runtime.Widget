/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Seo;

use Runtime.BaseModel;
use Runtime.VirtualDom;
use Runtime.Entity.Hook;
use Runtime.Hooks.BaseHook;
use Runtime.Hooks.RuntimeHook;
use Runtime.Web.RenderContainer;
use Runtime.Web.Hooks.AppHook;
use Runtime.Widget.Seo.SeoModel;


class Seo extends AppHook
{
	string class_name = classof SeoModel;
	
	
	/**
	 * Create hook
	 */
	static Hook hook(Map params = null) => new Hook(classof Seo, params);
	
	
	/**
	 * Init params
	 */
	void initParams(Map params)
	{
		parent(params)
		
		if (not params) return;
		if (params.has("class_name")) this.class_name = params.get("class_name");
	}
	
	
	/**
	 * Register hooks
	 */
	void register_hooks()
	{
		this.register(RuntimeHook::CREATE_LAYOUT, "createLayout");
		this.register(RuntimeHook::LAYOUT_HEADER, "renderHeader");
		this.register(AppHook::ROUTE_BEFORE, "routeBefore");
	}
	
	
	/**
	 * Create layout
	 */
	void createLayout(Map params)
	{
		RenderContainer container = params.get("container");
		container.layout.storage.set("seo", container.layout.createWidget(classof SeoModel));
	}
	
	
	/**
	 * Render head
	 */
	void renderHeader(Map params)
	{
		BaseModel layout = params.get("layout");
		SeoModel seo = layout.get("seo");
		if (seo) params.get("components").push(VirtualDom::renderModel(seo));
	}
	
	
	/**
	 * Route before
	 */
	void routeBefore(Map params)
	{
		RenderContainer container = params.get("container");
		
		/* If page model exists */
		if (container.route == null) return;
		if (container.route.data == null) return;
		
		/* Setup route data */
		SeoModel seo = container.layout.get("seo");
		Dict route_data = container.route.data;
		if (route_data.has("title")) container.layout.setPageTitle(route_data.get("title"));
		if (route_data.has("description")) seo.description = route_data.get("description");
		if (route_data.has("robots")) seo.robots = route_data.get("robots");
	}
}