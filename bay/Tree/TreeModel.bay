/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Tree;

use Runtime.BaseObject;
use Runtime.BaseModel;
use Runtime.Reference;
use Runtime.Serializer;
use Runtime.Serializer.ObjectType;
use Runtime.Web.Events;
use Runtime.Widget.ContextMenu.ContextMenuModel;
use Runtime.Widget.Tree.Dnd;
use Runtime.Widget.Tree.TreeItem;
use Runtime.Widget.Tree.TreeMessage;
use Runtime.Widget.Tree.TreeWidget;


class TreeModel extends BaseModel
{
	string component = classof TreeWidget;
	bool autoselect = true;
	bool icons = true;
	bool is_open = false;
	bool has_icons = false;
	bool context_menu_render = true;
	Dnd dnd = null;
	ContextMenuModel context_menu = null;
	Collection<int> selected_path = null;
	TreeItem selected_item = null;
	TreeItem root = null;
	
	
	/**
	 * Serialize object
	 */
	static void serialize(ObjectType rules)
	{
		parent(rules);
		rules.addType("root", new ObjectType{
			"autocreate": true,
			"extends": classof TreeItem
		});
	}
	
	
	/**
	 * Init widget params
	 */
	void initParams(Dict params)
	{
		parent(params);
		
		if (params == null) return;
		if (params.has("autoselect")) this.autoselect = params.get("autoselect");
		if (params.has("dnd"))
		{
			this.dnd = params.get("dnd");
			if (rtl::isBoolean(this.dnd))
			{
				if (this.dnd) this.dnd = new Dnd();
				else this.dnd = null;
			}
			if (this.dnd instanceof Dnd) this.dnd.model = this;
		}
		if (params.has("icons")) this.icons = params.get("icons");
	}
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		/* Setup context menu */
		if (params.has("context_menu"))
		{
			this.setContextMenu(params.get("context_menu"));
			if (params.has("context_menu_render"))
			{
				this.context_menu_render = params.get("context_menu_render");
			}
		}
	}
	
	
	/**
	 * Set context menu
	 */
	void setContextMenu(var context_menu)
	{
		if (context_menu instanceof ContextMenuModel)
		{
			this.context_menu_render = false;
			this.context_menu = context_menu;
		}
		else if (context_menu instanceof Map)
		{
			this.context_menu_render = true;
			this.context_menu = this.createWidget(
				classof ContextMenuModel,
				context_menu
			);
		}
	}
	
	
	/**
	 * Select item
	 */
	void selectItem(Collection<int> path)
	{
		TreeItem item = path ? this.root.get(path) : null;
		if (this.selected_item == item) return;
		
		this.selected_path = path;
		this.selected_item = item;
		if (this.selected_item)
		{
			this.selected_item.onSelect();
		}
	}
}
