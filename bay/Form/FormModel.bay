/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Form;

use Runtime.lib;
use Runtime.Callback;
use Runtime.ModelProxy;
use Runtime.Web.ApiResult;
use Runtime.Web.BaseRoute;
use Runtime.Web.BaseModel;
use Runtime.Web.Layout;
use Runtime.Widget.FieldInfo;
use Runtime.Widget.FieldList;
use Runtime.Widget.Crud.CrudDataStorage;
use Runtime.Widget.Crud.CrudWidget;
use Runtime.Widget.Form.Form;
use Runtime.Widget.Form.TopBackButton;
use Runtime.Widget.Table.TableModel;
use Runtime.Widget.TopButtonModel;


struct FormModel extends BaseModel
{
	static const string ON_LOAD_ITEM_BEFORE = "runtime.widget.form::load_item_before";
	static const string ON_LOAD_ITEM_AFTER = "runtime.widget.form::load_item_after";
	static const string ON_LOAD_DICTIONARY_BEFORE = "runtime.widget.form::load_dictionary_before";
	static const string ON_LOAD_DICTIONARY_AFTER = "runtime.widget.form::load_dictionary_after";
	static const string ON_SAVE_BEFORE = "runtime.widget.form::save_before";
	static const string ON_SAVE_AFTER = "runtime.widget.form::save_after";
	static const string ON_DELETE_BEFORE = "runtime.widget.form::delete_after";
	static const string ON_DELETE_AFTER = "runtime.widget.form::delete_after";
	
	string action = "";
	Dict search_data = null;
	Dict field_result = null;
	FieldList fields = new FieldList;
	CrudDataStorage data = new CrudDataStorage;
	bool is_loading = false;
	bool show_top_buttons = false;
	bool show_form_buttons = true;
	Callback get_names = null;
	string back_page_url = "";
	string component_name = classof Form;
	string widget_name = "form";
	bool can_dictionary_load = true;
	Collection top_buttons = [
		new TopButtonModel{ "component_name": classof TopBackButton, "name": "back" },
	];
	
	
	/**
	 * Set action
	 */
	FormModel setAction(string action) => this.clone({ "action": action });
	
	
	/**
	 * Set back page url
	 */
	FormModel setBackPage(string url) => this.clone{
		"back_page_url": url,
	};
	
	
	/**
	 * Set get names
	 */
	FormModel setGetNames(Callback value) => this.clone({ "get_names": value });
	
	
	/**
	 * Set search data
	 */
	FormModel setSearchData(Dict search_data) => this.clone({ "search_data": search_data });
	
	
	/**
	 * Show top buttons
	 */
	FormModel setShowTopButtons(bool value) => this.clone({ "show_top_buttons": value });
	
	
	/**
	 * Show form buttons
	 */
	FormModel setShowFormButtons(bool value) => this.clone({ "show_form_buttons": value });
	
	
	/**
	 * Add top button
	 */
	FormModel addTopButton(TopButtonModel model) => this.clone({
		"top_buttons": this.top_buttons.pushIm(model)
	});
	
	
	/**
	 * Clear error
	 */
	FormModel clearFieldErrors() => this.clone({ "field_result": null });
	
	
	/**
	 * Set loading
	 */
	FormModel setLoading(bool value) => this.clone({ "is_loading": value });
	
	
	/**
	 * Set dictionary load
	 */
	FormModel setDictionaryLoad(bool value) => this.clone({ "can_dictionary_load": value });
	
	
	/**
	 * Set data storage
	 */
	FormModel setDataStorage(CrudDataStorage storage)
		=> this.clone({ "data": storage });
	
	
	/**
	 * Set api result
	 */
	static void setApiResult(ModelProxy model, string event, ApiResult res)
	{
		if (res == null) return;
		
		/* Set validation result */
		if (res.data != null and res.data.has("field_result"))
		{
			model.commit("setFieldResult", res.data["field_result"]);
		}
		else
		{
			model.commit("setFieldResult", null);
		}
		
		/* Change action */
		if (event == "save" and res.isSuccess())
		{
			bool is_new = model.proxy("data").call("isNew");
			if (is_new)
			{
				model.commit("setAction", "add");
			}
			else
			{
				model.commit("setAction", "edit");
			}
		}
	}
	
	
	/**
	 * Returns form class
	 */
	string getErrorClass()
	{
		if (this.data.error_message == "") return "web_form__result--hide";
		if (this.data.error_code > 0) return "web_form__result--success";
		if (this.data.error_code < 0) return "web_form__result--error";
		return "";
	}
	
	
	/**
	 * Get field result
	 */
	Collection<string> getFieldResult(string field_name)
	{
		if (this.field_result == null) return [];
		if (not this.field_result.has(field_name)) return [];
		return this.field_result[field_name];
	}
	
	
	/**
	 * Set field result
	 */
	FormModel setFieldResult(Dict field_result) => this.clone{
		"field_result": field_result,
	};
	
	
	/**
	 * Add crud widget
	 */
	static async ModelProxy addCrudWidget(
		ModelProxy<FormModel> model, ModelProxy<Layout> layout, Dict params
	)
	{
		string widget_name = params["widget_name"];
		string crud_model_name = params["crud_model_name"] |> default string classof CrudWidget;
		Dict crud_model_params = params["crud_model_params"] |> default Dict {};
		string foreign_key = params["foreign_key"] |> default string "id";
		string primary_key = params["primary_key"] |> default string "id";
		
		crud_model_params <= widget_name <= widget_name;
		
		/* Add widget */
		CrudWidget crud_widget_item = rtl::newInstance(
			crud_model_name, [crud_model_params]
		);
		ModelProxy crud_widget = model.call("addWidget", crud_widget_item);
		
		/* Add form */
		ModelProxy widget_form = crud_widget.proxy(["widgets", "form"]);
		Callback form_model = params["form_model"];
		if (form_model != null)
		{
			crud_widget.call("addWidget", form_model.call());
		}
		
		string form_model_name = params["form_model_name"];
		if (form_model_name != null)
		{
			crud_widget.call("addWidget", rtl::newInstance(form_model_name));
		}
		
		/* Add table */
		ModelProxy widget_table = crud_widget.proxy(["widgets", "table"]);
		Callback table_model = params["table_model"];
		if (table_model != null)
		{
			crud_widget.call("addWidget", table_model.call());
		}
		
		string table_model_name = params["table_model_name"];
		if (table_model_name != null)
		{
			crud_widget.call("addWidget", rtl::newInstance(table_model_name));
		}
		
		/* Init crud widget */
		await crud_widget.commitAsync("initWidget", layout);
		
		/* Add components */
		layout.commit("addComponent", classof TopBackButton);
		
		/* Set foreign key */
		var value_id = (int)model.data(["data", "item_pk", primary_key]);
		widget_form.proxy("data").commit("setForeignKey", foreign_key, value_id);
		widget_table.proxy("data").commit("setForeignKey", foreign_key, value_id);
		
		/* Remove foreign key */
		widget_form.proxy("fields").commit("removeFieldFromList", foreign_key);
		widget_table.proxy("fields").commit("removeFieldFromList", foreign_key);
		
		return crud_widget;
	}
	
	
	/**
	 * Load widget
	 */
	async static void loadWidget(ModelProxy model, ModelProxy layout)
	{
		string action = model.data("action");
		ModelProxy data = model.proxy("data");
		data.commit("clearItem");
		
		if (action == "add")
		{
			await model.commitAsync("doLoadDictionary");
		}
		else if (action == "edit" or action == "delete")
		{
			await model.commitAsync("doLoadItem");
		}
		
		await parent::loadWidget(model, layout);
	}
	
	
	/**
	 * Widget event
	 */
	async static Dict event
	(
		ModelProxy<BaseModel> widget,
		string action, Dict obj, var tag = null
	)
	{
		return obj;
	}
	
	
	/**
	 * Load dictionary
	 */
	async static ApiResult doLoadDictionary(ModelProxy model)
	{
		/* Get data storage */
		ModelProxy data = model.proxy("data");
		if (not data.call("isReady")) return;
		if (not model.data("can_dictionary_load")) return;
		
		/* Is dictionary exists */
		if (model.data(["fields", "dictionary_list"]).count() == 0) return;
		
		/* Setup loading */
		model.commit("setLoading", true);
		
		/* Get data storage */
		ModelProxy data = model.proxy("data");
		ModelProxy fields = model.proxy("fields");
		
		/* Get post data */
		Dict search_data = model.data("search_data");
		Dict post_data = data.call("getItemPostData", search_data);
		
		/* Before event */
		Dict d = await static::callEvent(model, static::ON_LOAD_DICTIONARY_BEFORE, {
			"post_data": post_data,
			"success": true,
		});
		
		if (not d["success"]) return;
		
		/* Send api */
		ApiResult res = await data.callAsync("callApiDictionary", d["post_data"]);
		
		/* Set api result */
		fields.commit("setApiResult", "dictionary", res);
		model.commit("setApiResult", "dictionary", res);
		
		/* Set loading */
		model.commit("setLoading", false);
		
		/* After event */
		await static::callEvent(model, static::ON_LOAD_DICTIONARY_AFTER, {
			"api": res,
			"post_data": d["post_data"],
		});
	}
	
	
	/**
	 * Load item
	 */
	async static ApiResult doLoadItem(ModelProxy model)
	{
		/* Get data storage */
		ModelProxy data = model.proxy("data");
		if (not data.call("isReady")) return;
		
		/* Setup loading */
		model.commit("setLoading", true);
		
		/* Get data storage */
		ModelProxy data = model.proxy("data");
		ModelProxy fields = model.proxy("fields");
		
		/* Get post data */
		Dict search_data = model.data("search_data");
		Dict post_data = data.call("getItemPostData", search_data);
		
		/* Before event */
		Dict d = await static::callEvent(model, static::ON_LOAD_ITEM_BEFORE, {
			"post_data": post_data,
			"success": true,
		});
		
		if (not d["success"]) return;
		
		/* Send api */
		ModelProxy data = model.proxy("data");
		ApiResult res = await data.callAsync("callApiItem", d["post_data"]);
		
		/* Set api result */
		data.commit("setApiResult", "item", res);
		fields.commit("setApiResult", "item", res);
		model.commit("setApiResult", "item", res);
		
		/* Set loading */
		model.commit("setLoading", false);
		
		/* Clear message if success */
		if (res.isSuccess())
		{
			data.commit("clearErrorMessage");
		}
		
		/* After event */
		await static::callEvent(model, static::ON_LOAD_ITEM_AFTER, {
			"api": res,
			"post_data": d["post_data"],
		});
	}
	
	
	/**
	 * Save form
	 */
	async static ApiResult doSave(ModelProxy model)
	{
		/* Get data storage */
		ModelProxy data = model.proxy("data");
		if (not data.call("isReady")) return;
		
		Dict item_pk = model.data("item_pk");
		Dict item = model.data("item");
		
		/* Get data storage */
		ModelProxy data = model.proxy("data");
		
		/* Get post data */
		Dict search_data = model.data("search_data");
		Dict post_data = data.call("getSavePostData", search_data);
		
		/* Before event */
		Dict d = await static::callEvent(model, static::ON_SAVE_BEFORE, {
			"post_data": post_data,
			"success": true,
		});
		
		if (not d["success"]) return;
		
		/* Send api */
		ModelProxy data = model.proxy("data");
		ApiResult res = await data.callAsync("callApiSave", d["post_data"]);
		
		/* Set api result */
		data.commit("setApiResult", "save", res);
		model.commit("setApiResult", "save", res);
		
		/* After event */
		await static::callEvent(model, static::ON_SAVE_AFTER, {
			"api": res,
			"post_data": d["post_data"],
		});
	}
	
	
	/**
	 * Delete item
	 */
	async static ApiResult doDelete(ModelProxy model)
	{
		/* Get data storage */
		ModelProxy data = model.proxy("data");
		if (not data.call("isReady")) return;
		
		/* Get data storage */
		ModelProxy data = model.proxy("data");
		
		/* Get post data */
		Dict search_data = model.data("search_data");
		Dict post_data = data.call("getItemPostData", search_data);
		
		/* Before event */
		Dict d = await static::callEvent(model, static::ON_DELETE_BEFORE, {
			"post_data": post_data,
			"success": true,
		});
		
		if (not d["success"]) return;
		
		/* Send api */
		ModelProxy data = model.proxy("data");
		ApiResult res = await data.callAsync("callApiDelete", d["post_data"]);
		
		/* Set api result */
		data.commit("setApiResult", "delete", res);
		model.commit("setApiResult", "delete", res);
		
		/* After event */
		Dict d = await static::callEvent(model, static::ON_DELETE_AFTER, {
			"api": res,
			"post_data": d["post_data"],
		});
	}
}
