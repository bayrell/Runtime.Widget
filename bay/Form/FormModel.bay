/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Form;

use Runtime.ApiResult;
use Runtime.BaseModel;
use Runtime.Method;
use Runtime.Message;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.ObjectType;
use Runtime.Widget.ResultModel;
use Runtime.Widget.Form.FieldErrors;
use Runtime.Widget.Form.Form;


class FormModel extends BaseModel
{
	string component = classof Form;
	Map pk = null;
	Map item = {};
	Map field_errors = null;
	ResultModel result = null;
	BaseType primary_rules = null;
	BaseType item_rules = null;
	
	
	/**
	 * Serialize object
	 */
	static void serialize(ObjectType rules)
	{
		parent(rules);
		rules.addType("pk", rules.params ? rules.params.get("primary_rules") : null);
		rules.addType("item", rules.params ? rules.params.get("item_rules") : null);
		rules.addType("result", new ObjectType{"class_name": classof ResultModel});
		rules.setup.add(void (FormModel model, ObjectType rules){
			model.primary_rules = rules.params ? rules.params.get("primary_rules") : null;
			model.item_rules = rules.params ? rules.params.get("item_rules") : null;
		});
	}
	
	
	/**
	 * Init params
	 */
	void initParams(Map params)
	{
		parent(params);
	}
	
	
	/**
	 * Init widget
	 */
	void initWidget(Map params)
	{
		parent(params);
		
		this.field_errors = this.createWidget(classof FieldErrors, {
			"error_name": "fields",
		});
		this.result = this.createWidget(classof ResultModel);
	}
	
	
	/**
	 * Set wait message
	 */
	void setWaitMessage()
	{
		this.result.setWaitMessage();
	}
	
	
	/**
	 * Set api result
	 */
	void setApiResult(ApiResult result)
	{
		this.result.setApiResult(result);
		this.field_errors.setApiResult(result);
	}
	
	
	/**
	 * Returns result
	 */
	ResultModel getResult(string name)
	{
		return this.field_errors.get(name);
	}
	
	
	/**
	 * Set item value
	 */
	void setValue(string name, var value)
	{
		this.item.set(name, value);
	}
	
	
	/**
	 * Set primary key
	 */
	void setPrimaryKey(Map item)
	{
		if (this.primary_rules)
		{
			Vector primary_key = this.primary_rules.keys();
			this.pk = this.primary_rules.filter(item.intersect(primary_key), []);
		}
		else
		{
			this.pk = null;
		}
	}
	
	
	/**
	 * Set item
	 */
	void setItem(var item)
	{
		this.item = this.item_rules ? this.item_rules.filter(item, []) : item;
	}
	
	
	/**
	 * Clear form
	 */
	void clear()
	{
		this.pk = null;
		this.item = this.data_object ? rtl::newInstance(this.data_object) : {};
		this.field_errors.clear();
		this.result.clear();
	}
}