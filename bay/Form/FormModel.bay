/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Form;

use Runtime.ApiResult;
use Runtime.BaseModel;
use Runtime.Method;
use Runtime.Message;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.ObjectType;
use Runtime.Widget.ResultModel;
use Runtime.Widget.Form.Form;


class FormModel extends BaseModel
{
	string component = classof Form;
	Map pk = null;
	Map item = {};
	Map field_errors = {};
	ResultModel result = null;
	BaseType pk_type = null;
	BaseType item_type = null;
	
	
	/**
	 * Serialize object
	 */
	static void serialize(ObjectType rules)
	{
		parent(rules);
		rules.addType("pk", rules.params ? rules.params.get("pk_type") : null);
		rules.addType("item", rules.params ? rules.params.get("item_type") : null);
		rules.addType("result", new ObjectType{"class_name": classof ResultModel});
		rules.setup.add(void (FormModel model, ObjectType rules){
			model.pk_type = rules.params ? rules.params.get("pk_type") : null;
			model.item_type = rules.params ? rules.params.get("item_type") : null;
		});
	}
	
	
	/**
	 * Init params
	 */
	void initParams(Map params)
	{
		parent(params);
	}
	
	
	/**
	 * Init widget
	 */
	void initWidget(Map params)
	{
		parent(params);
		
		this.result = this.createWidget(classof ResultModel);
	}
	
	
	/**
	 * Set wait message
	 */
	void setWaitMessage()
	{
		this.result.setWaitMessage();
	}
	
	
	/**
	 * Set api result
	 */
	void setApiResult(ApiResult result)
	{
		this.result.setApiResult(result);
		this.setFieldErrors(result.data.get("fields"));
	}
	
	
	/**
	 * Set field errors
	 */
	void setFieldErrors(Map field_errors)
	{
		if (not field_errors) return;
		
		this.field_errors = {};
		Vector keys = rtl::list(field_errors.keys());
		for (int i=0; i<keys.count(); i++)
		{
			string field_name = keys.get(i);
			Vector messages = field_errors.get(field_name);
			string message = rs::join(", ", messages);
			ResultModel result = new ResultModel();
			result.setError(message);
			this.field_errors.set(field_name, result);
		}
	}
	
	
	/**
	 * Returns result
	 */
	void getResult(string name)
	{
		return this.field_errors.get(name);
	}
	
	
	/**
	 * Set item value
	 */
	void setValue(string name, var value)
	{
		this.item.set(name, value);
	}
	
	
	/**
	 * Set primary key
	 */
	void setPrimaryKey(Map item)
	{
		if (this.pk_type)
		{
			Vector primary_key = this.pk_type.keys();
			this.pk = this.pk_type.filter(item.intersect(primary_key), []);
		}
		else
		{
			this.pk = null;
		}
	}
	
	
	/**
	 * Set item
	 */
	void setItem(var item)
	{
		this.item = this.item_type ? this.item_type.filter(item, []) : item;
	}
	
	
	/**
	 * Clear form
	 */
	void clear()
	{
		this.pk = null;
		this.item = this.data_object ? rtl::newInstance(this.data_object) : {};
		this.field_errors = {};
		this.result.clear();
	}
}