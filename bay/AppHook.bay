/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget;

use Runtime.BaseLayout;
use Runtime.Hooks.RuntimeHook;
use Runtime.Web.RenderContainer;
use Runtime.Web.Hooks.AppHook as WebHook;
use Runtime.Widget.Assets;
use Runtime.Widget.CSS;


class AppHook extends RuntimeHook
{
	const string ASSETS = "runtime.widget::assets";
	const string INIT_WIDGET = "baylang::init_widget";
	
	
	/**
	 * Register hooks
	 */
	void register_hooks()
	{
		this.register(RuntimeHook::COMPONENTS, "components", 50);
		this.register(WebHook::ROUTE_BEFORE, "registerAssets");
		this.register(static::INIT_WIDGET, "initWidget");
	}
	
	
	/**
	 * Register assets
	 */
	async void registerAssets(Map params)
	{
		RenderContainer container = params.get("container");
		BaseLayout layout = container.layout;
		if (not layout) return;
		
		/* Create assets */
		Assets assets = new Assets();
		layout.storage.set("assets", assets);
		
		/* Init assets */
		@.hook(static::ASSETS, {"assets": assets});
	}
	
	
	/**
	 * Components
	 */
	void components(Map params)
	{
		Vector components = [
			classof CSS,
		];
		components.appendItems(params.get("components"));
		params.set("components", components);
	}
	
	
	/**
	 * Init widget
	 */
	void initWidget(Map params)
	{
		string widget_class = "BayLang.Constructor.Frontend.Editor.Widget.WidgetComponent";
		var provider = params.get("provider");
		
		/* Section */
		provider.registerWidget({
			"name": "section",
			"label": "Section",
			"component": "Runtime.Widget.Section",
			"widget": widget_class,
			"props": {},
		});
	}
}