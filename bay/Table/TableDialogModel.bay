/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Table;

use Runtime.Callback;
use Runtime.Exceptions.RuntimeException;
use Runtime.Web.RenderListModel;
use Runtime.Web.Messages.ClickMessage;
use Runtime.Widget.ButtonModel;
use Runtime.Widget.Crud.CrudApiStorage;
use Runtime.Widget.Form.FormMessage;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.RowButtons;
use Runtime.Widget.RowButtonsModel;
use Runtime.Widget.Table.DeleteDialogModel;
use Runtime.Widget.Table.SaveDialogModel;
use Runtime.Widget.Table.TableModel;


class TableDialogModel extends TableModel
{
	FormModel form;
	SaveDialogModel save_dialog;
	DeleteDialogModel delete_dialog;
	fn get_title = null;
	
	
	/**
	 * Init widget params
	 */
	void initParams(Dict params)
	{
		parent(params);
		
		if (params == null) return;
		if (params.has("get_title")) this.get_title = params.get("get_title");
		
		/* Form */
		if (params.has("add_form"))
		{
			this.add_form = this.createModel(params.get("add_form"));
		}
		if (params.has("edit_form"))
		{
			this.edit_form = this.createModel(params.get("edit_form"));
		}
		if (params.has("delete_form"))
		{
			this.delete_form = this.createModel(params.get("delete_form"));
		}
		
		/* Setup dialog */
		if (params.has("save_dialog"))
		{
			this.save_dialog = this.createModel(params.get("save_dialog"));
		}
		if (params.has("delete_dialog"))
		{
			this.delete_dialog = this.createModel(params.get("delete_dialog"));
		}
	}
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		/* Add events */
		this.add_form.addListener("submit", new Callback(this, "onFormSubmit"));
		this.edit_form.addListener("submit", new Callback(this, "onFormSubmit"));
		this.delete_form.addListener("submit", new Callback(this, "onFormSubmit"));
		
		/* Create save wiget */
		this.save_dialog = this.addWidget(
			classof SaveDialogModel,
			{
				"add_form": this.add_form,
				"edit_form": this.edit_form,
				"table": this,
				"widget_name": "save_dialog",
			}
		);
		
		/* Create delete widget */
		this.delete_dialog = this.addWidget(
			classof DeleteDialogModel,
			{
				"form": this.delete_form,
				"table": this,
				"widget_name": "delete_dialog",
			}
		);
		
		/* Add dialogs */
		this.render_list.addItem(this.save_dialog);
		this.render_list.addItem(this.delete_dialog);
	}
	
	
	/**
	 * Returns title
	 */
	string getTitle(Dict params)
	{
		if (this.get_title)
		{
			return rtl::apply(this.get_title, [params]);
		}
		return "";
	}
	
	
	/**
	 * On form submit
	 */
	void onFormSubmit(FormMessage message)
	{
		if (message.result.isSuccess())
		{
			this.reload();
		}
	}
	
	
	/**
	 * Row button click
	 */
	void onRowButtonClick(ClickMessage message)
	{
		Dict data = message.data;
		string button_name = message.widget.widget_name;
		
		if (button_name == "edit_button")
		{
			this.save_dialog.showEdit(data.get("item"));
		}
		else if (button_name == "delete_button")
		{
			this.delete_dialog.showDelete(data.get("item"));
		}
	}
}