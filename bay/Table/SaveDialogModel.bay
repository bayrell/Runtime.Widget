/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Table;

use Runtime.BaseObject;
use Runtime.Callback;
use Runtime.Serializer;
use Runtime.Web.ApiResult;
use Runtime.Web.BaseModel;
use Runtime.Web.ModelFactory;
use Runtime.Web.Messages.ClickMessage;
use Runtime.Widget.Button;
use Runtime.Widget.ButtonModel;
use Runtime.Widget.Dialog.ConfirmDialogModel;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Table.SaveDialog;
use Runtime.Widget.Table.TableModel;


class SaveDialogModel extends ConfirmDialogModel
{
	string action = "";
	string widget_name = "save_dialog";
	string component = classof SaveDialog;
	FormModel add_form;
	FormModel edit_form;
	TableModel table;
	
	
	/**
	 * Init widget params
	 */
	void initParams(Dict params)
	{
		parent(params);
		
		if (params == null) return;
		if (params.has("action")) this.action = params.get("action");
		if (params.has("table")) this.table = params.get("table");
	}
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		if (params.has("add_form"))
		{
			if (params.get("add_form") instanceof ModelFactory)
			{
				this.add_form = params.get("add_form").factory(this);
			}
			else
			{
				this.add_form = params.get("add_form");
			}
		}
		if (params.has("edit_form"))
		{
			if (params.get("edit_form") instanceof ModelFactory)
			{
				this.edit_form = params.get("edit_form").factory(this);
			}
			else
			{
				this.edit_form = params.get("edit_form");
			}
		}
	}
	
	
	/**
	 * Add close button click
	 */
	void onCloseButtonClick(ClickMessage message)
	{
		parent();
		if (this.action == "add" and this.add_form) this.add_form.setItem(null);
		if (this.action == "edit" and this.edit_form) this.edit_form.setItem(null);
	}
}
