/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Table;

use Runtime.ApiResult;
use Runtime.BaseModel;
use Runtime.Message;
use Runtime.Method;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.MapType;
use Runtime.Serializer.ObjectType;
use Runtime.Web.RenderContainer;
use Runtime.Widget.Dialog.DialogModel;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Table.TableLoader;
use Runtime.Widget.Table.TableModel;


class TableManager extends BaseModel
{
	FormModel form = null;
	TableModel table = null;
	string api_name = "";
	string page_name = "page";
	Vector form_fields = [];
	Vector table_fields = [];
	Map foreign_key = null;
	MapType foreign_rules = null;
	MapType item_rules = null;
	MapType primary_rules = null;
	
	/* Dialog */
	string dialog_action = "";
	DialogModel dialog = null;
	TableLoader loader = null;
	
	/* Functions */
	fn title = null;
	
	
	/**
	 * Serialize object
	 */
	static void serialize(ObjectType rules)
	{
		parent(rules);
		rules.addType("foreign_key", rules.params ? rules.params.get("foreign_key") : null);
		rules.addType("form", new ObjectType{
			"autocreate": true,
			"extends": classof FormModel,
			"primary_rules": rules.params ? rules.params.get("primary_rules") : null,
			"item_rules": rules.params ? rules.params.get("item_rules") : null,
		});
		rules.addType("table", new ObjectType{
			"autocreate": true,
			"extends": classof TableModel,
			"item_rules": rules.params ? rules.params.get("item_rules") : null,
		});
		rules.addType("dialog", new ObjectType{ "extends": classof DialogModel });
		rules.addType("loader", new ObjectType{
			"extends": classof TableLoader,
			"foreign_key": rules.params ? rules.params.get("foreign_key") : null,
		});
		rules.setup.add(void (TableManager model, ObjectType rules){
			rules.addType("foreign_key", model.foreign_rules);
			
			ObjectType form = rules.items.get("form").get(0);
			form.params.set("primary_rules", model.primary_rules);
			form.params.set("item_rules", model.item_rules);
			
			ObjectType table = rules.items.get("table").get(0);
			table.params.set("item_rules", model.item_rules);
			
			ObjectType loader = rules.items.get("loader").get(0);
			loader.params.set("foreign_key", model.foreign_rules);
		});
	}
	
	
	/**
	 * Init params
	 */
	void initParams(Map params)
	{
		parent(params);
		
		if (params.has("api_name")) this.api_name = params.get("api_name");
		if (params.has("foreign_key")) this.foreign_key = params.get("foreign_key");
		if (params.has("foreign_rules")) this.foreign_rules = params.get("foreign_rules");
		if (params.has("item_rules"))
		{
			this.item_rules = params.get("item_rules");
			if (this.item_rules instanceof Map) this.item_rules = new MapType(this.item_rules);
		}
		if (params.has("primary_rules"))
		{
			this.primary_rules = params.get("primary_rules");
			if (this.primary_rules instanceof Map) this.primary_rules = new MapType(this.primary_rules);
		}
		if (params.has("page_name")) this.page_name = params.get("page_name");
		if (params.has("title")) this.title = params.get("title");
		if (this.foreign_rules instanceof Map) this.foreign_rules = new MapType(this.foreign_rules);
	}
	
	
	/**
	 * Init widget
	 */
	void initWidget(Map params)
	{
		parent(params);
		
		if (params.has("dialog")) this.dialog = params.get("dialog");
		else this.dialog = this.createWidget(classof DialogModel);
		this.dialog.addEventListener("hide", new Method(this, "onDialogHide"));
		
		if (params.has("form")) this.form = params.get("form");
		else this.form = this.createWidget(classof FormModel);
		
		if (params.has("table")) this.table = params.get("table");
		else this.table = this.createWidget(classof TableModel);
		
		if (params.has("loader")) this.loader = params.get("loader");
		else this.loader = this.createWidget(classof TableLoader, {
			"api_name": this.api_name,
			"foreign_key": this.foreign_key,
			"page_name": this.page_name,
			"table": this.table,
		});
		
		if (params.has("form_fields")) this.form_fields = params.get("form_fields");
		if (params.has("table_fields")) this.table_fields = params.get("table_fields");
		
		/* Add fields */
		Vector fields = this.form_fields ? this.form_fields : [];
		if (this.table_fields) fields = fields.concat(this.table_fields);
		fields.each(void (Map field){
			if (not field.has("component")) return;
			this.layout.components.push(field.get("component"));
		});
	}
	
	
	/**
	 * Load data
	 */
	async void loadData(RenderContainer container)
	{
		await this.loader.loadData(container);
	}
	
	
	/**
	 * Set foreign key
	 */
	void setForeignKey(Map key)
	{
		this.foreign_key = key;
		this.loader.foreign_key = key;
	}
	
	
	/**
	 * Returns dialog action
	 */
	string getDialogAction()
	{
		if (this.dialog_action == "save")
		{
			if (this.form.pk) return "edit";
			else return "add";
		}
		return this.dialog_action;
	}
	
	
	/**
	 * Returns dialog title
	 */
	string getDialogTitle(string action)
	{
		if (this.title) return rtl::apply(this.title, [action, this.form.item]);
		return "";
	}
	
	
	/**
	 * Show add dialog
	 */
	void showAddDialog()
	{
		this.dialog_action = "save";
		this.form.clear();
		this.dialog.show();
	}
	
	
	/**
	 * Show edit dialog
	 */
	void showEditDialog(Map item)
	{
		this.dialog_action = "save";
		this.form.clear();
		this.form.setPrimaryKey(item);
		this.form.setItem(item);
		this.dialog.show();
	}
	
	
	/**
	 * Show delete dialog
	 */
	void showDeleteDialog(Map item)
	{
		this.dialog_action = "delete";
		this.form.clear();
		this.form.setPrimaryKey(item);
		this.form.setItem(item);
		this.dialog.show();
	}
	
	
	/**
	 * On hide dialog
	 */
	void onDialogHide()
	{
		this.dialog_action = "";
	}
	
	
	/**
	 * On save
	 */
	async void onSave()
	{
		this.form.setWaitMessage();
		ApiResult result = await this.layout.sendApi({
			"api_name": this.api_name,
			"method_name": "save",
			"data": {
				"pk": this.form.pk,
				"item": this.form.item,
				"foreign_key": this.foreign_key,
			},
		});
		this.form.setApiResult(result);
		if (result.isSuccess())
		{
			this.loader.reload();
			this.dialog.hide();
		}
	}
	
	
	/**
	 * On delete
	 */
	async void onDelete()
	{
		this.form.setWaitMessage();
		ApiResult result = await this.layout.sendApi({
			"api_name": this.api_name,
			"method_name": "delete",
			"data": {
				"pk": this.form.pk,
				"foreign_key": this.foreign_key,
			},
		});
		this.form.setApiResult(result);
		if (result.isSuccess())
		{
			this.loader.reload();
			this.dialog.hide();
		}
	}
}