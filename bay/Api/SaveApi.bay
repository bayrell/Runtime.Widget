/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Api;

use Runtime.Serializer;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.AssertException;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.ORM.Connection;
use Runtime.ORM.Query;
use Runtime.ORM.QueryFilter;
use Runtime.ORM.Record;
use Runtime.ORM.Relation;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.TypeError;
use Runtime.Web.BaseApi;
use Runtime.Widget.Api.Rules.BaseRule;


class SaveApi extends BaseApi
{
	string action = "";
	string connection_name = "";
	Connection connection = null;
	Relation relation = null;
	Map pk = null;
	Record item = null;
	Map original_data = {};
	Map update_data = {};
	Map field_errors = {};
	
	
	/**
	 * Returns relation name
	 */
	string getRealtionName() => "";
	
	
	/**
	 * Constructor
	 */
	void constructor(Map params)
	{
		parent(params);
		
		this.result.data.set("fields", this.field_errors);
		this.connection = Connection::getConnection(this.connection_name);
		this.relation = rtl::newInstance(this.getRealtionName(), [{
			"connection": this.connection,
		}]);
	}
	
	
	/**
	 * Init params
	 */
	void initParams(Map params)
	{
		parent(params);
		if (not params) return;
		if (params.has("action")) this.action.setAction(params.get("action"));
		if (params.has("connection_name")) this.connection_name = params.get("connection_name");
	}
	
	
	/**
	 * Returns save rules
	 */
	Vector<BaseRule> rules() => [];
	
	
	/**
	 * Returns serialize rules for pk
	 */
	BaseType getPrimaryRules() => null;
	
	
	/**
	 * Returns serialize rules
	 */
	BaseType getType() => null;
	
	
	/**
	 * Returns item fields
	 */
	Vector getItemFields() => [];
	
	
	/**
	 * Convert item
	 */
	Map convertItem(Map item) => item;
	
	
	/**
	 * Add field errors
	 */
	void addFieldErrors(Vector errors)
	{
		for (int i=0; i<errors.count(); i++)
		{
			TypeError error = errors.get(i);
			if (not this.field_errors.has(error.field_name))
			{
				this.field_errors.set(error.field_name, []);
			}
			Vector item = this.field_errors.get(error.field_name);
			item.push(error.message);
		}
	}
	
	
	/**
	 * Filter primary key
	 */
	Map getPrimaryKey(Map pk)
	{
		Vector errors = [];
		
		/* Get primary key rule */
		BaseType rule = this.getPrimaryRules();
		if (not rule)
		{
			throw new ApiError(new RuntimeException("Primary rule not found"));
		}
		
		/* Filter primary key */
		pk = rule.filter(pk, errors, null);
		
		/* Check errors */
		if (errors.count() > 0)
		{
			this.fail({"message": "Fields error"});
			this.addFieldErrors(errors);
			throw new ApiError(this.result);
		}
		return pk;
	}
	
	
	/**
	 * Set action
	 */
	void setAction(string action)
	{
		this.action = action;
	}
	
	
	/**
	 * Set primary key
	 */
	void setPrimaryKey(Map pk)
	{
		if (not (pk instanceof Map))
		{
			throw new ApiError(new RuntimeException("Primary key not found"));
		}
		
		this.pk = this.getPrimaryKey(pk);
	}
	
	
	/**
	 * Set data
	 */
	void setData(Map data)
	{
		if (not (data instanceof Map)) data = {};
		
		this.original_data = data;
		this.update_data = {};
		
		/* Get base type rule */
		BaseType rule = this.getType();
		if (not rule)
		{
			throw new ApiError(new RuntimeException("Base type not found"));
		}
		
		/* Filter */
		Vector errors = [];
		this.update_data = rule.filter(data, errors, null);
		
		/* Rules */
		Vector rules = this.rules();
		for (int i=0; i<rules.count(); i++)
		{
			BaseRule item = rules.get(i);
			item.filter(this.update_data, errors);
		}
		
		/* Check error */
		if (errors.count() > 0)
		{
			this.fail({"message": "Fields error"});
			this.addFieldErrors(errors);
			throw new ApiError(this.result);
		}
	}
	
	
	/**
	 * Build query
	 */
	async void buildQuery(Query q)
	{
	}
	
	
	/**
	 * Find item
	 */
	async void findItem(Map pk)
	{
		if (pk == null) return;
		
		Vector<QueryFilter> filter = this.relation.getPrimaryFilter(pk);
		Query q = this.relation.select().setFilter(filter).limit(1);
		await this.buildQuery(q);
		this.item = await this.relation.fetch(q);
	}
	
	
	/**
	 * Create item
	 */
	void createItem()
	{
		this.item = this.relation.createRecord();
	}
	
	
	/**
	 * Find or create item
	 */
	async void findOrCreate()
	{
		await this.findItem(this.pk);
		if (not this.item and this.pk == null) this.createItem();
		if (not this.item) throw new ApiError(new ItemNotFound());
	}
	
	
	/**
	 * Save item
	 */
	async void save()
	{
		this.item.assign(this.update_data);
		await this.relation.save(this.item);
	}
	
	
	/**
	 * Delete item
	 */
	async void delete()
	{
		await this.relation.delete(this.item);
	}
	
	
	/**
	 * Set result
	 */
	void setResult()
	{
		if (this.action == "save")
		{
			Map item = this.item.getData();
			item = this.convertItem(item);
			this.result.data.set("item", item.intersect(this.getItemFields()));
		}
	}
}